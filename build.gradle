buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.51"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'

version = "1.0.0"
group = "therealfarfetchd.terminator"
archivesBaseName = "terminator"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}
compileKotlin {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12.2-14.23.0.2501"
    runDir = "run"
    mappings = "snapshot_20170624"
}

repositories {
    jcenter()
    maven {
        url "https://modmaven.k-4u.nl/"
    }
    maven {
        url "http://maven.shadowfacts.net/"
    }
}

dependencies {
    compile projectOrMvn(":QuackLib", "quacklib:quacklib:+")

    compile group: "net.shadowfacts", name: "Forgelin", version: "1.5.1"
}

def projectOrMvn(String project, String artifact) {
    Project p = findProject(project)
    if (p != null)
        dependencies { compile p }
    else
        dependencies { deobfCompile artifact }
    []
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
